#!/bin/zsh

# Copyright 2015 Han Boetes <han@boetes.org>
# For license info see http://unlicense.org/

# This script plays songs, random or non-random, see the -h output for
# more help.

# Feature requests: play songs only once, add optional count in queue mode
# Make it usable for other people.

PATH=/bin:/usr/local/bin:/usr/bin
setopt no_bare_glob_qual
alias GREP='grep -i'
playlist="/mp3/playlist.gz"
play_queue="/mp3/queue"
# Number of track in the queue to print
qhist=10
qfuture=20
play_hist="/mp3/history"
# Directory where you want to store your pidfile.
prog="${0##*/}"
pid_dir="$TMPDIR/${prog}-$(id -u)"
# Number of songs to play, unless overriden later on.
count=1

# Ugly code for portability.
OS=$(uname -s)
case $OS in
    Darwin)
        ZCAT=gzcat
        ;;
    *)
        ZCAT=zcat
        ;;
esac


alnum()
{
    if [[ $1 != <-> ]]; then
        error "'$1' is not a valid integer!"
    fi
}

print_green()
{
    print '\e[32m'${1}'\e[00m'
}

print_yellow()
{
    print '\e[33m'${1}'\e[00m'
}

print_song()
{
    song_nopath="${song:t}"
    clear
    readh
    if ((${#h} > $qhist)); then
        pr=$qhist
    else
        pr=${#h}
    fi
    print -l ${h[-$pr,-1]:t}
    print_green $song_nopath
    h=($h $song_nopath)
    writeh
    print "NP: $song_nopath" > ~/.nowplaying
    rsync ~/.nowplaying han@boetes.org: &
    # Let's not print to much files in the queue
    readq
    print -l ${q[1,$qfuture]:t}
    qlength=${#q}
    if ((qlength > qfuture)); then
        ((rest = qlength - qfuture))
        print_yellow "And $rest more tracks."
    fi
}

unprint_song()
{
    : > ~/.nowplaying
    rsync ~/.nowplaying han@boetes.org: &
}

warn()
{
    print "$prog: Warning: Song $song not found!?" >&2
}

error()
{
    print "$prog: $1" >&2
    exit 1
}

play_song()
{
    print_song
    # "-vo null" disables displaying album art
    mpv  -vo null -af volume=-10 "$song" -really-quiet || warn
}

create_pid()
{
    # First create the pid_dir in a safe way, if it doesn't exist.
    if [[ ! -d $pid_dir ]]; then
        pid_tmpdir=$(mktemp -d $pid_dir.XXXXXXXXXXXXXXXXX) || error "Can't create tmp dir."
        mv $pid_tmpdir $pid_dir || error "Can't create pid dir."
    fi

    if [[ -e $pid_dir/pid ]]; then
        error "is already running."
    else
        # And then create a pid_file
        print $$ > $pid_dir/pid
    fi
}

remove_pid()
{
    # Only remove the pidfile if it belongs to the current
    # process. This is redundant hopefully. :-}
    pid=$(< $pid_dir/pid)
    if [[ $$ = $pid ]]; then
        rm $pid_dir/pid
    fi
}

readq()
{
    if [[ ! -f $play_queue ]]; then
        error "$play_queue does not exist or isn't a regular file."
    fi
    q=("${(@f)$(< $play_queue)}") || error "Can't read $play_queue"

}

writeq()
{
    print -l $q > $play_queue || error "Can't write to ${play_queue}, check permissions."
}

# XXX Can we deduplicate this?
readh()
{
    # We'll create this file if it doesn't exist
    touch $play_hist
    if [[ ! -f $play_hist ]]; then
        error "$play_hist does not exist or isn't a regular file."
    fi
    h=("${(@f)$(< $play_hist)}") || error "Can't read $play_hist"

}

writeh()
{
    print -l $h > $play_hist || error "Can't write to ${play_hist}, check permissions."
}

queue_mode()
{
    create_pid
    while [[ -s $play_queue ]]; do
        # Keep reading the $queue_file, the user may edit it while $0 is running.
        readq
        song=${q[1]}
        q[1]=()
        writeq
        play_song
        if [[ $pause = "TRUE" ]]; then
            print -n 'Press the any key to continue '
            read -sk 1 nothing
            print
        fi
    done
    # This message only makes sense in queue mode.
    if [[ $qmode == "TRUE" ]]; then
        print "The queue is empty!"
    fi
    exit 0
}

enqueue_song()
{
    readq
    if [[ $head == "TRUE" ]]; then
        q[$headcount,0]=($song)
        ((headcount++))
    else
        q=($q $song)
    fi
    writeq
}

zshexit()
{
    if [[ $do_nothing != "TRUE" ]]; then
        unprint_song
        remove_pid
    fi
    exit
}

# Incase the program is interrupted, for example by pressing ctrl-c
# first make sure the pid file is removed etc.
TRAPINT()
{
    zshexit
}

show_help()
{
    cat << EOF
Usage: $prog -[ncaplLqxH] [grep regex]
  -n play tracks in non-random order
  -c x count
  -a play all selected files.
  -p pause after each track.
  -l select regex with finetuning.
  -L = -l -a -n
  -q play all songs from $play_queue
  -x clear the queue
  -H add songs to the head of $play_queue
EOF
}

# Parse command line arguments
while  (($# > 0)); do
    case $1 in
        -h)
            show_help
            do_nothing="TRUE"
            exit 0
            ;;
        -q)
            # No other arguments. Just play the queue until the end.
            unset nonrandom get_list pause all
            qmode="TRUE"
            queue_mode
            ;;
        -n)
            shift 1
            nonrandom="TRUE"
            ;;
        -c)
            alnum $2
            count=$2
            shift 2
            ;;
        -p)
            pause="TRUE"
            shift 1
            ;;
        -l)
            get_list="TRUE"
            shift 1
            ;;
        -L)
            get_list="TRUE"
            all="TRUE"
            nonrandom="TRUE"
            shift 1
            ;;
        -a)
            all="TRUE"
            shift 1
            ;;
        -x)
            # Clear the queue
            : > $play_queue
            shift 1
            ;;

        -H)
            head="TRUE"
            headcount=1
            shift 1
            ;;
        -*)
            print "Unknown option: $1" >&2
            show_help
            do_nothing="TRUE"
            exit 1
            ;;
        *)
            # Handle input regex below.
            break
            ;;
    esac
done

# What are we going to play?
searchstring="$*"

# This is a bit of zsh magic: array=("${(@f)$(command)}") it ensures a
# list of files is turned into a proper array.
allfiles=("${(@f)$($ZCAT $playlist)}")

# A regex has been given and we can refine that regex and see the
# results.
if [[ $get_list = "TRUE" ]]; then
    while :; do
        if [[ $searchstring == "" ]]; then
            searchstring='.'
        fi
        selection=("${(@f)$(print -l $allfiles | GREP $searchstring)}")
        clear
        print -l $selection
        oldsearchstring="$searchstring"
        # vared is a very nifty function provided by zsh.
        vared -p 'What do you want to hear? Press enter if you are done. ' searchstring
        if [[ $oldsearchstring = $searchstring ]]; then
            allfiles=($selection)
            break
        fi
    done
elif [[ $searchstring == "" ]]; then
    # No searchstring given, that's a valid option too. Just play
    # something.
    :
elif ! allfiles=("${(@f)$(print -l $allfiles | GREP $searchstring)}"); then
    # Or a regex is given but no need to refine it.
    do_nothing="TRUE"
    error "Nothing found with that regexp."
fi

if [[ $all = "TRUE" ]]; then
    count=${#allfiles}
fi

holdcount="$count"
# Get through our list.
while ((count-- > 0)); do
    if [[ ${#allfiles} == 0 ]]; then
        print 'No more files to play.'
        break
    fi
    if [[ $nonrandom == "TRUE" ]]; then
        rndm=1
    else
        # The Pseudorandom $RANDOM is not random enough. All songs
        # from the same few artists gets annoying real quick.
        rndm=$(od -vAn -N4 -tu4 < /dev/urandom)
        # Mind the off by one.
        (((rndm %= ${#allfiles}) += 1))
    fi
    song="$allfiles[$rndm]"
    enqueue_song
    # Remove the current song from the playlist.
    allfiles[$rndm]=()
done

# Probably you want to start playing music if you get here and $0
# isn't running.
if [[ ! -f $pid_dir/pid ]] && [[ -s $play_queue ]]; then
    queue_mode
else
    if [[ $head == "TRUE" ]]; then
        WHERE=head
    else
        WHERE=bottom
    fi
    print "$holdcount songs added to the $WHERE of the play queue file."
    do_nothing="TRUE"
fi
